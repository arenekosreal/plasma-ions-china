if(NOT QT_MAJOR_VERSION)
    set(QT_MAJOR_VERSION 6)
endif()
if(QT_MAJOR_VERSION LESS 6)
    message(FATAL_ERROR "Qt < 6 is not supported.")
endif()

set(_PLASMA_WEATHER_DATAENGINE_LAST_VERSION 6.4.5) # 6.4.5 is the last KDE6 version using DataEngine.
set(_PLASMA_WEATHER_QT_MIN_VERSION 6.5.0) # 6.5.0 is the first used Qt6 version of KDE6.

include(FindPackageHandleStandardArgs)

function(extract_soname SOPATH SONAME_VAR)
    file(REAL_PATH "${SOPATH}" SOPATH_REAL)
    string(REGEX MATCH "[0-9]+\\.[0-9]+\\.[0-9]+$" SONAME "${SOPATH_REAL}")
    set(${SONAME_VAR} "${SONAME}" PARENT_SCOPE)
endfunction()

function(find_component COMPONENT HEADER LIBRARY TARGET_PREFIX REQUIRED)
    set(${CMAKE_FIND_PACKAGE_NAME}_${COMPONENT}_FOUND FALSE PARENT_SCOPE)
    find_path(_${COMPONENT}_INCLUDE_ROOT "${HEADER}")
    find_library(_${COMPONENT}_LIBRARY "${LIBRARY}")
    if(_${COMPONENT}_INCLUDE_ROOT AND _${COMPONENT}_LIBRARY)
        cmake_path(REMOVE_FILENAME HEADER OUTPUT_VARIABLE _${COMPONENT}_INCLUDE_RELATIVE_DIR)
        cmake_path(ABSOLUTE_PATH _${COMPONENT}_INCLUDE_ROOT)
        cmake_path(ABSOLUTE_PATH _${COMPONENT}_LIBRARY)
        extract_soname("${_${COMPONENT}_LIBRARY}" _${COMPONENT}_VERSION)
        add_library(${TARGET_PREFIX}::${COMPONENT} SHARED IMPORTED GLOBAL)
        set_target_properties(${TARGET_PREFIX}::${COMPONENT}
                              PROPERTIES IMPORTED_LOCATION "${_${COMPONENT}_LIBRARY}"
                                         INTERFACE_INCLUDE_DIRECTORIES "${_${COMPONENT}_INCLUDE_ROOT}/${_${COMPONENT}_INCLUDE_RELATIVE_DIR}")
        find_package_check_version("${_${COMPONENT}_VERSION}" _${COMPONENT}_VERSION_CHECK_RESULT
                                   HANDLE_VERSION_RANGE
                                   RESULT_MESSAGE_VARIABLE _${COMPONENT}_VERSION_CHECK_RESULT_MESSAGE)
        if(_${COMPONENT}_VERSION_CHECK_RESULT)
            if(NOT ${CMAKE_FIND_PACKAGE_NAME}_FIND_QUIETLY)
                message(STATUS "Found ${CMAKE_FIND_PACKAGE_NAME} ${COMPONENT}: ${_${COMPONENT}_LIBRARY} ${_${COMPONENT}_VERSION_CHECK_RESULT_MESSAGE}")
            endif()
            set(${CMAKE_FIND_PACKAGE_NAME}_${COMPONENT}_LIBRARY "${_${COMPONENT}_LIBRARY}" PARENT_SCOPE)
            set(${CMAKE_FIND_PACKAGE_NAME}_${COMPONENT}_INCLUDE_DIR "${_${COMPONENT}_INCLUDE_ROOT}/${_${COMPONENT}_INCLUDE_RELATIVE_DIR}" PARENT_SCOPE)
            set(${CMAKE_FIND_PACKAGE_NAME}_${COMPONENT}_VERSION "${_${COMPONENT}_VERSION}" PARENT_SCOPE)
            set(${CMAKE_FIND_PACKAGE_NAME}_${COMPONENT}_FOUND TRUE PARENT_SCOPE)
            set(${CMAKE_FIND_PACKAGE_NAME}_LIBRARIES ${${CMAKE_FIND_PACKAGE_NAME}_LIBRARIES} "${_${COMPONENT}_LIBRARY}" PARENT_SCOPE)
            set(${CMAKE_FIND_PACKAGE_NAME}_INCLUDE_DIRS ${${CMAKE_FIND_PACKAGE_NAME}_INCLUDE_DIRS} "${_${COMPONENT}_INCLUDE_ROOT}/${_${COMPONENT}_INCLUDE_RELATIVE_DIR}" PARENT_SCOPE)
        elseif(${REQUIRED})
            message(FATAL_ERROR "${CMAKE_FIND_PACKAGE_NAME} ${COMPONENT} was not found: ${_${COMPONENT}_VERSION_CHECK_RESULT_MESSAGE}")
        else()
            message(WARNING "${CMAKE_FIND_PACKAGE_NAME} ${COMPONENT} was not found: ${_${COMPONENT}_VERSION_CHECK_RESULT_MESSAGE}")
        endif()
    endif()
endfunction()

find_package_check_version("${_PLASMA_WEATHER_DATAENGINE_LAST_VERSION}" _PLASMA_WEATHER_LEGACY_ALLOWED
                           HANDLE_VERSION_RANGE)

if(Ion IN_LIST "${PlasmaWeather_FIND_COMPONENTS}" OR PlasmaWeather_FIND_REQUIRED_Ion)
    find_component(Ion "plasma/weatherion/ion.h" "plasmaweatherion" Plasma::Weather $<NOT:$<_PLASMA_WEATHER_LEGACY_ALLOWED>>)
    if(TARGET Plasma::Weather::Ion)
        set(PlasmaWeather_Ion_LEGACY FALSE)
        set(PlasmaWeather_FIND_REQUIRED_Data TRUE)
        list(APPEND PlasmaWeather_FIND_COMPONENTS Data)
    elseif(_PLASMA_WEATHER_LEGACY_ALLOWED)
        set(PlasmaWeather_Ion_LEGACY TRUE)
        set(PlasmaWeather_FIND_REQUIRED_IonLegacy TRUE)
        set(PlasmaWeather_FIND_REQUIRED_Ion FALSE)
        list(REMOVE_ITEM PlasmaWeather_FIND_COMPONENTS Ion)
        list(APPEND PlasmaWeather_FIND_COMPONENTS IonLegacy)
        message(STATUS "Unable to find Ion but IonLegacy is allowed, replacing Ion with IonLegacy...")
    else()
        message(FATAL_ERROR "${CMAKE_FIND_PACKAGE_NAME} Ion was not found: No suitable library.")
    endif()
endif()

if(IonLegacy IN_LIST "${PlasmaWeather_FIND_COMPONENTS}" OR PlasmaWeather_FIND_REQUIRED_IonLegacy)
    find_component(IonLegacy "plasma5support/weather/ion.h" "weather_ion" Plasma::Weather ${PlasmaWeather_FIND_REQUIRED_IonLegacy})
    if(TARGET Plasma::Weather::IonLegacy)
        find_package(Plasma5Support REQUIRED)
        target_link_libraries(Plasma::Weather::IonLegacy INTERFACE Plasma::Plasma5Support)
        if(NOT TARGET Plasma::Weather::Ion)
            add_library(Plasma::Weather::Ion ALIAS Plasma::Weather::IonLegacy)
            set(PlasmaWeather_Ion_LIBRARY "${PlasmaWeather_IonLegacy_LIBRARY}")
            set(PlasmaWeather_Ion_INCLUDE_DIR "${PlasmaWeather_IonLegacy_INCLUDE_DIR}")
            set(PlasmaWeather_Ion_VERSION "${PlasmaWeather_IonLegacy_VERSION}")
            set(PlasmaWeather_Ion_FOUND TRUE)
        endif()
    endif()
endif()

if(Data IN_LIST "${PlasmaWeather_FIND_COMPONENTS}" OR PlasmaWeather_FIND_REQUIRED_Data)
    find_component(Data "plasma/weatherdata/plasmaweatherdata_export.h" "plasmaweatherdata" Plasma::Weather ${PlasmaWeather_FIND_REQUIRED_Data})
    if(TARGET Plasma::Weather::Data)
        find_package(Qt${QT_MAJOR_VERSION} ${_PLASMA_WEATHER_QT_MIN_VERSION} COMPONENTS Qml REQUIRED)
        target_link_libraries(Plasma::Weather::Data INTERFACE Qt::Qml)
        if(TARGET Plasma::Weather::Ion AND NOT TARGET Plasma::Weather::IonLegacy)
            target_link_libraries(Plasma::Weather::Ion INTERFACE Plasma::Weather::Data)
        endif()
    endif()
endif()

set(PlasmaWeather_VERSION ${PlasmaWeather_Ion_VERSION})
find_package_handle_standard_args(PlasmaWeather
                                  VERSION_VAR PlasmaWeather_VERSION
                                  HANDLE_VERSION_RANGE
                                  HANDLE_COMPONENTS)

mark_as_advanced(_PLASMA_WEATHER_DATAENGINE_LAST_VERSION _PLASMA_WEATHER_LEGACY_ALLOWED _PLASMA_WEATHER_QT_MIN_VERSION)
